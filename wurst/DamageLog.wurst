/*
	This example package stores up to STORED_INSTANCES last damage events
	and prints them up when host presses `ESC` button.
*/
package DamageLog
import DamageModify

// config //

constant STORED_INSTANCES = 16

// end of config //

Damage array storedDamage
int storedIndex

init
	addDamageListener() d ->
		d.saveInstance()
		if storedDamage[storedIndex] != null
			storedDamage[storedIndex].releaseInstance()
		storedDamage[storedIndex] = d
		storedIndex += 1
		if storedIndex > STORED_INSTANCES - 1
			storedIndex = 0
			
	// Press `ESC` to see saved instances
	CreateTrigger()..registerPlayerEvent(players[0], EVENT_PLAYER_END_CINEMATIC).addAction() ->
		var x = storedIndex
		for i = 0 to STORED_INSTANCES - 1
			let d = storedDamage[x]
			if d != null
				var s = "Damage #"+i.toString()
				s += " was dealt by "+d.source.getName()
				s += " to "+d.target.getName()
				s += " in amount of "+d.getActual().toString()
				s += " (intended: "+d.amount.toString()
				s += ", initial: " + d.initial.toString()
				s += ", killing blow: "+d.isKillingBlow().toString()
				s += ", spell damage: "+d.spell.toString()
				s += ")"
				Log.info(s)
			x += 1
			if x > STORED_INSTANCES - 1
				x = 0